#!/usr/bin/env groovy

def actions

pipeline {
    agent {
        label 'docker'
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Prepare') {
            steps {
                dir ('results') {
                    deleteDir()
                }
                script {
                    actions = load 'sources/tests/actions.groovy'
                    actions.prepareSources('nexus.enalean.com_readonly', 'github-token-composer')
                }
            }
        }

        stage('Check lockfiles') {
            steps { script {
                actions.runFilesStatusChangesDetection('.', 'lockfiles', 'package-lock.json composer.lock')
            } }
            post {
                failure {
                    dir ('sources') {
                        sh 'git diff'
                    }
                }
            }
        }

        stage('Tests') {
            failFast false
            parallel {
                stage('UT SimpleTest PHP 7.3') {
                    steps { script { actions.runSimpleTestTests('73') } }
                    post { always { junit 'results/ut-simpletest/php-73/results.xml' } }
                }
                stage('UT PHPUnit') {
                    stages {
                        stage('UT PHPUnit PHP 7.3') {
                            steps { script { actions.runPHPUnitTests('73') } }
                        }
                    }
                    post { always { junit 'results/ut-phpunit/*/phpunit_tests_results.xml' } }
                }
                stage ('Jest') {
                    agent {
                        docker {
                            image 'node:13.3-alpine'
                            reuseNode true
                            args '--network none'
                        }
                    }
                    steps { script { actions.runJestTests('Tuleap', '.') } }
                    post {
                        always {
                            junit 'results/jest/test-*-results.xml'
                            publishCoverage adapters: [istanbulCoberturaAdapter('results/jest/coverage/cobertura-coverage.xml')], tag: 'Javascript'
                        }
                    }
                }
                stage('REST') {
                    stages {
                        stage('REST PHP 7.3 MySQL 5.7') {
                            steps { script { actions.runRESTTests('mysql57', '73') } }
                        }
                    }
                    post { always { junit 'results/api-rest/*/rest_tests.xml' } }
                }
                stage('DB Integration') {
                    stages {
                        stage('DB CentOS 6 PHP 7.3 MySQL 5.7') {
                            steps { script { actions.runDBTests('mysql57', '73') } }
                        }
                    }
                    post { always { junit 'results/db/*/db_tests.xml' } }
                }
                stage('SOAP') {
                    stages {
                        stage('SOAP PHP 7.3') { steps { script { actions.runSOAPTests('mysql57', '73') } } }
                    }
                    post { always { junit "results/soap/*/soap_tests.xml" } }
                }
                stage('Build and run CentOS 6') { steps { script { actions.runBuildAndRun('centos6') } } }
                stage('Build and run CentOS 7') { steps { script { actions.runBuildAndRun('centos7') } } }
                stage('Code conformity') {
                    stages {
                        stage('Check translation files') {
                            steps { script {
                                actions.runFilesStatusChangesDetection('.', 'translation files', '"*.po\$"')
                            } }
                        }
                        stage('ESLint static analysis') {
                            agent {
                                docker {
                                    image 'node:13.3-alpine'
                                    reuseNode true
                                    args '--network none'
                                }
                            }
                            steps { script { actions.runESLint() } }
                            post {
                                always {
                                    recordIssues enabledForFailure: true, tools: [checkStyle(id: 'checkstyle_eslint', pattern: 'results/eslint/checkstyle.xml')]
                                }
                            }
                        }
                        stage('PHP coding standards') {
                            steps {
                                script {
                                    def filesToAnalyze = '.'
                                    if (env.GIT_BRANCH != 'origin/master') {
                                        def helpers = load 'sources/tests/helpers.groovy'
                                        filesToAnalyze = helpers.getModifiedFilesSinceFirstParentOfCurrentCommit('sources/')
                                    }
                                    actions.runPHPCodingStandards(
                                        './src/vendor/bin/phpcs',
                                        './tests/phpcs/tuleap-ruleset-minimal.xml',
                                        filesToAnalyze
                                    )
                                }
                            }
                        }
                        stage('Psalm static analysis') {
                            agent {
                                dockerfile {
                                    dir 'sources/tests/psalm/'
                                    reuseNode true
                                    args '--network none'
                                }
                            }
                            steps {
                                script {
                                    def filesToAnalyze = '.'
                                    if (env.GIT_BRANCH != 'origin/master') {
                                        def helpers = load 'sources/tests/helpers.groovy'
                                        filesToAnalyze = helpers.getModifiedFilesSinceFirstParentOfCurrentCommit('sources/')
                                    }
                                    actions.runPsalm('tests/psalm/psalm.xml', filesToAnalyze)
                                }
                            }
                            post {
                                always {
                                    recordIssues enabledForFailure: true, minimumSeverity: 'NORMAL', tools: [checkStyle(id: 'checkstyle_psalm', pattern: 'results/psalm/checkstyle.xml')]
                                }
                            }
                        }
                        stage('Architecture') {
                            agent {
                                docker {
                                    image 'php:7.4-cli-alpine'
                                    reuseNode true
                                    args '--network none'
                                }
                            }
                            steps { script { actions.runDeptrac('tests/deptrac/core_on_plugins.yml', 'core_on_plugins') } }
                            post { failure { junit 'results/deptrac/*.xml' } }
                        }
                    }
                }
            }
            post {
                always {
                    archiveArtifacts allowEmptyArchive: true, artifacts: 'results/'
                }
            }
        }
    }
}
