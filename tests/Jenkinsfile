#!/usr/bin/env groovy

// See https://issues.jenkins.io/browse/JENKINS-44085 and
// and https://github.com/jenkinsci/bom/blob/542369a68d4c8b604626b4dbc00a109cc8833836/Jenkinsfile#L47-L71
semaphore_max_test_stages = 7;
def execTestWithLimitedConcurrency(Closure testToExecute) {
    waitUntil { if (semaphore_max_test_stages > 0) { semaphore_max_test_stages--; true } else { false } }
    assert semaphore_max_test_stages >= 0
    try {
        testToExecute.call()
    } finally {
        semaphore_max_test_stages++
    }
}

def actions
def helpers

pipeline {
    agent {
        label 'docker'
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Setup Nix cache') {
            steps {
                script {
                    helpers = load 'sources/tests/helpers.groovy'
                    helpers.authenticateOnTailscale()
                    helpers.setupTuleapCommunityNixCache()
                }
            }
        }

        stage('Prepare') {
            steps {
                dir ('results') {
                    deleteDir()
                }
                script { actions = load 'sources/tests/actions.groovy' }
                dir ('sources') {
                    withCredentials([
                        usernamePassword(
                            credentialsId: 'nexus.enalean.com_readonly',
                            passwordVariable: 'NPM_PASSWORD',
                            usernameVariable: 'NPM_USER'
                        ),
                        string(credentialsId: 'github-token-composer', variable: 'COMPOSER_GITHUB_AUTH')
                    ]) {
                        script { actions.prepareSources('dev') }
                    }
                }
            }
        }

        stage('Check lockfiles') {
            steps { script {
                actions.runFilesStatusChangesDetection('.', 'lockfiles', 'composer.lock')
            } }
            post {
                failure {
                    dir ('sources') {
                        sh 'git diff'
                    }
                }
            }
        }

        stage('Tests') {
            failFast false
            parallel {
                stage('UT PHPUnit PHP 8.4') {
                    steps { script { execTestWithLimitedConcurrency({ actions.runPHPUnitTests('84') }) } }
                    post { always { junit 'results/ut-phpunit/*/phpunit_tests_results.xml' } }
                }
                stage('REST PHP 8.4 MySQL 8.4') {
                    steps { script { execTestWithLimitedConcurrency({ actions.runRESTTests('mysql84', '84') }) } }
                    post { always { junit 'results/api-rest/*/*' } }
                }
                stage('DB/Integration PHP 8.4 MySQL 8.4') {
                    steps { script { execTestWithLimitedConcurrency({ actions.runDBTests('mysql84', '84') }) } }
                    post { always { junit 'results/db/*/db_tests.xml' } }
                }
                stage('Build and run el9') { steps { script { execTestWithLimitedConcurrency({ actions.runBuildAndRun('el9') }) } } }
                stage('Translations: check .po files') {
                    steps { script {
                        actions.runFilesStatusChangesDetection('.', 'translation files', '"*.po\$"')
                    } }
                }
                stage('Translations: check .tab files') {
                    steps { script { execTestWithLimitedConcurrency({ actions.runTabFilesCheck() }) } }
                }
                stage('Psalm static analysis') {
                    agent {
                        docker {
                            image 'ghcr.io/enalean/tuleap-aio-dev:el9-php84'
                            reuseNode true
                            args '--network none'
                        }
                    }
                    steps {
                        script {
                            execTestWithLimitedConcurrency({
                                actions.runPsalm('tests/psalm/psalm.xml')
                        }) }
                    }
                    post {
                        always {
                            recordIssues enabledForFailure: true, minimumSeverity: 'NORMAL', tools: [checkStyle(id: 'checkstyle_psalm', pattern: 'results/psalm/checkstyle.xml')]
                        }
                    }
                }
                stage('Architecture: deptrac') {
                    agent {
                        docker {
                            image 'ghcr.io/enalean/tuleap-aio-dev:el9-php84'
                            reuseNode true
                            args '--network none'
                        }
                    }
                    steps { script { execTestWithLimitedConcurrency({ actions.runDeptrac() }) } }
                    post { always { junit 'results/deptrac/*.xml' } }
                }
                stage ('JS Unit tests') {
                    steps { script { execTestWithLimitedConcurrency({ actions.runJSUnitTests() }) } }
                    post { always { junit 'results/js-test-results/junit-*.xml' } }
                }
                stage ('Check consistency JS manifest files') {
                    steps { script { execTestWithLimitedConcurrency({ actions.runJSManifestConsistencyCheck() }) } }
                }
                stage ('Scan for secrets') {
                    steps { script { execTestWithLimitedConcurrency({ actions.runSecretsScan() }) } }
                }
                stage ('Treefmt linter/formatter') {
                    steps {
                        script {
                            execTestWithLimitedConcurrency({
                                def filesToAnalyze = '.'
                                if (env.GIT_BRANCH != 'origin/master') {
                                    filesToAnalyze = helpers.getModifiedFilesSinceFirstParentOfCurrentCommit('sources/')
                                }
                                actions.runTreefmt(filesToAnalyze)
                            })
                        }
                    }
                }
                stage ('Verify licenses') {
                    steps { script { execTestWithLimitedConcurrency({ actions.verifyLicenses() }) } }
                }
            }
            post {
                always {
                    archiveArtifacts allowEmptyArchive: true, artifacts: 'results/'
                }
            }
        }
    }
}
